program Tut09;

{$mode delphi}

uses {$IFDEF UNIX} {$IFDEF UseCThreads}
    cthreads, {$ENDIF} {$ENDIF}
    Interfaces, // this includes the adLCL widgetset
    MultiMon,
    SysUtils,
    Windows,
    DX12.D3D12,
    DX12.D3DX12,
    DX12.DXGI,
    DX12.DXGI1_4,
    DX12.DXGI1_3,
    DirectX.Math,
    DX12.D3D12SDKLayers,
    DX12.D3DCommon,
    DX12.D3DCompiler;

{$R *.res}

const
    // name of the window (not the title)
    WindowName: WideString = 'BzTutsApp';

    // title of the window
    WindowTitle: WideString = 'Bz Window';

const
    frameBufferCount = 3; // number of buffers we want, 2 for double buffering, 3 for tripple buffering


type

    TVertex = record
        pos: TXMFLOAT3;
        color: TXMFLOAT4;
    end;


    // this is the structure of our constant buffer.
    TConstantBufferPerObject = record
        wvpMat: TXMFLOAT4X4;
    end;




var
    // Handle to the window
    Handle: HWnd = 0;
    Running: boolean = True;

    // width and height of the window
    Width: integer = 800;
    Height: integer = 600;

    // is window full screen?
    FullScreen: boolean = False;

    // direct3d stuff
    device: ID3D12Device; // direct3d device
    swapChain: IDXGISwapChain3; // swapchain used to switch between render targets
    commandQueue: ID3D12CommandQueue; // container for command lists
    rtvDescriptorHeap: ID3D12DescriptorHeap; // a descriptor heap to hold resources like the render targets
    renderTargets: array [0..frameBufferCount - 1] of ID3D12Resource; // number of render targets equal to buffer count
    commandAllocator: array [0..frameBufferCount - 1] of ID3D12CommandAllocator;
    // we want enough allocators for each buffer * number of threads (we only have one thread)
    commandList: ID3D12GraphicsCommandList; // a command list we can record commands into, then execute them to render the frame
    fence: array [0..frameBufferCount - 1] of ID3D12Fence;
    // an object that is locked while our command list is being executed by the gpu. We need as many
    //as we have allocators (more if we want to know when the gpu is finished with an asset)
    fenceEvent: THANDLE; // a handle to an event when our fence is unlocked by the gpu
    fenceValue: array [0..frameBufferCount - 1] of UINT64; // this value is incremented each frame. each fence will have its own value
    frameIndex: integer; // current rtv we are on
    rtvDescriptorSize: integer; // size of the rtv descriptor on the device (all front and back buffers will be the same size)

    pipelineStateObject: ID3D12PipelineState = nil; // pso containing a pipeline state

    rootSignature: ID3D12RootSignature = nil; // root signature defines data shaders will access

    viewport: TD3D12_VIEWPORT; // area that output from rasterizer will be stretched to.

    scissorRect: TD3D12_RECT; // the area to draw in. pixels outside that area will not be drawn onto

    vertexBuffer: ID3D12Resource; // a default buffer in GPU memory that we will load vertex data for our triangle into
    indexBuffer: ID3D12Resource; // a default buffer in GPU memory that we will load index data for our triangle into

    vertexBufferView: TD3D12_VERTEX_BUFFER_VIEW; // a structure containing a pointer to the vertex data in gpu memory
    // the total size of the buffer, and the size of each element (vertex)
    indexBufferView: TD3D12_INDEX_BUFFER_VIEW; // a structure holding information about the index buffer

    depthStencilBuffer: ID3D12Resource; // This is the memory for our depth buffer. it will also be used for a stencil buffer in a later tutorial
    dsDescriptorHeap: ID3D12DescriptorHeap; // This is a heap for our depth/stencil buffer descriptor

    // Constant buffers must be 256-byte aligned which has to do with constant reads on the GPU.
    // We are only able to read at 256 byte intervals from the start of a resource heap, so we will
    // make sure that we add padding between the two constant buffers in the heap (one for cube1 and one for cube2)
    // Another way to do this would be to add a float array in the constant buffer structure for padding. In this case
    // we would need to add a float padding[50]; after the wvpMat variable. This would align our structure to 256 bytes (4 bytes per float)
    // The reason i didn't go with this way, was because there would actually be wasted cpu cycles when memcpy our constant
    // buffer data to the gpu virtual address. currently we memcpy the size of our structure, which is 16 bytes here, but if we
    // were to add the padding array, we would memcpy 64 bytes if we memcpy the size of our structure, which is 50 wasted bytes
    // being copied.
    ConstantBufferPerObjectAlignedSize: integer;//  = (sizeof(ConstantBufferPerObject) + 255) & ~255;

    cbPerObject: TConstantBufferPerObject; // this is the constant buffer data we will send to the gpu
    // (which will be placed in the resource we created above)

    constantBufferUploadHeaps: array [0..frameBufferCount - 1] of ID3D12Resource;
    // this is the memory on the gpu where constant buffers for each frame will be placed

    cbvGPUAddress: array[0..frameBufferCount - 1] of PByte; // this is a pointer to each of the constant buffer resource heaps

    cameraProjMat: TXMFLOAT4X4; // this will store our projection matrix
    cameraViewMat: TXMFLOAT4X4; // this will store our view matrix

    cameraPosition: TXMFLOAT4; // this is our cameras position vector
    cameraTarget: TXMFLOAT4; // a vector describing the point in space our camera is looking at
    cameraUp: TXMFLOAT4; // the worlds up vector

    cube1WorldMat: TXMFLOAT4X4; // our first cubes world matrix (transformation matrix)
    cube1RotMat: TXMFLOAT4X4; // this will keep track of our rotation for the first cube
    cube1Position: TXMFLOAT4; // our first cubes position in space

    cube2WorldMat: TXMFLOAT4X4; // our first cubes world matrix (transformation matrix)
    cube2RotMat: TXMFLOAT4X4; // this will keep track of our rotation for the second cube
    cube2PositionOffset: TXMFLOAT4; // our second cube will rotate around the first cube, so this is the position offset from the first cube

    numCubeIndices: integer; // the number of indices to draw the cube



    procedure Safe_Release(var i);
    begin
        if IUnknown(i) <> nil then
            IUnknown(i) := nil;
    end;



    function WndProc(Handle: HWND; msg: UINT; wParam: WPARAM; lParam: LPARAM): LRESULT; stdcall;
    begin
        case (msg) of
            WM_KEYDOWN:
            begin
                if (wParam = VK_ESCAPE) then
                begin
                    if (MessageBox(0, 'Are you sure you want to exit?', 'Really?', MB_YESNO or MB_ICONQUESTION) = idYes) then
                    begin
                        Running := False;
                        DestroyWindow(Handle);
                    end;
                end;
                Result := 0;
            end;

            WM_DESTROY:
            begin
                Running := False;
                PostQuitMessage(0);
                Result := 0;
            end
            else
                Result := DefWindowProc(Handle, msg, wParam, lParam);
        end;
    end;



    function InitializeWindow(Instance: PtrUInt; ShowWnd: integer; Width: integer; Height: integer; fullscreen: boolean): boolean;
    var
        hMon: HMonitor;
        mi: TMONITORINFO;
        wc: WNDCLASSEXW;
    begin
        Result := False;
        if (fullscreen) then
        begin
            hmon := MonitorFromWindow(Handle, MONITOR_DEFAULTTONEAREST);
            mi.cbSize := sizeof(mi);
            GetMonitorInfo(hmon, @mi);

            Width := mi.rcMonitor.right - mi.rcMonitor.left;
            Height := mi.rcMonitor.bottom - mi.rcMonitor.top;
        end;

        wc.cbSize := sizeof(WNDCLASSEXW);
        wc.style := CS_HREDRAW or CS_VREDRAW;
        wc.lpfnWndProc := @WndProc;
        wc.cbClsExtra := 0;
        wc.cbWndExtra := 0;
        wc.hInstance := hInstance;
        wc.hIcon := LoadIcon(0, IDI_APPLICATION);
        wc.hCursor := LoadCursor(0, IDC_ARROW);
        wc.hbrBackground := (COLOR_WINDOW + 2);
        wc.lpszMenuName := nil;
        wc.lpszClassName := PWideChar(WindowName);
        wc.hIconSm := LoadIcon(0, IDI_APPLICATION);

        if (RegisterClassExW(wc) = 0) then
        begin
            MessageBox(0, 'Error registering class', 'Error', MB_OK or MB_ICONERROR);
            Exit;
        end;

        Handle := CreateWindowExW(0, PWideChar(WindowName), PWideChar(WindowTitle), WS_OVERLAPPEDWINDOW, CW_USEDEFAULT,
            CW_USEDEFAULT, Width, Height, 0, 0, hInstance, nil);

        if (Handle = 0) then
        begin
            MessageBox(0, 'Error creating window', 'Error', MB_OK or MB_ICONERROR);
            Exit;
        end;

        if (fullscreen) then
        begin
            SetWindowLong(Handle, GWL_STYLE, 0);
        end;

        ShowWindow(Handle, ShowWnd);
        UpdateWindow(Handle);

        Result := True;
    end;



    function InitD3D(): boolean;
    const
        inputLayout: array [0..1] of TD3D12_INPUT_ELEMENT_DESC =
            ((SemanticName: 'POSITION'; SemanticIndex: 0; Format: DXGI_FORMAT_R32G32B32_FLOAT; InputSlot: 0; AlignedByteOffset: 0;
            InputSlotClass: D3D12_INPUT_CLASSIFICATION_PER_VERTEX_DATA; InstanceDataStepRate: 0),
            (SemanticName: 'COLOR'; SemanticIndex: 0; Format: DXGI_FORMAT_R32G32B32A32_FLOAT; InputSlot: 0; AlignedByteOffset: 12;
            InputSlotClass: D3D12_INPUT_CLASSIFICATION_PER_VERTEX_DATA; InstanceDataStepRate: 0));

        // a quad
        vList: array [0..23] of TVertex = (
            // front face
            (pos: (x: -0.5; y: 0.5; z: -0.5); color: (r: 1.0; g: 0.0; b: 0.0; a: 1.0)),
            (pos: (x: 0.5; y: -0.5; z: -0.5); color: (r: 1.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: -0.5; y: -0.5; z: -0.5); color: (r: 0.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: 0.5; y: 0.5; z: -0.5); color: (r: 0.0; g: 1.0; b: 0.0; a: 1.0)),

            // right side face
            (pos: (x: 0.5; y: -0.5; z: -0.5); color: (r: 1.0; g: 0.0; b: 0.0; a: 1.0)),
            (pos: (x: 0.5; y: 0.5; z: 0.5); color: (r: 1.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: 0.5; y: -0.5; z: 0.5); color: (r: 0.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: 0.5; y: 0.5; z: -0.5); color: (r: 0.0; g: 1.0; b: 0.0; a: 1.0)),

            // left side face
            (pos: (x: -0.5; y: 0.5; z: 0.5); color: (r: 1.0; g: 0.0; b: 0.0; a: 1.0)),
            (pos: (x: -0.5; y: -0.5; z: -0.5); color: (r: 1.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: -0.5; y: -0.5; z: 0.5); color: (r: 0.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: -0.5; y: 0.5; z: -0.5); color: (r: 0.0; g: 1.0; b: 0.0; a: 1.0)),

            // back face
            (pos: (x: 0.5; y: 0.5; z: 0.5); color: (r: 1.0; g: 0.0; b: 0.0; a: 1.0)),
            (pos: (x: -0.5; y: -0.5; z: 0.5); color: (r: 1.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: 0.5; y: -0.5; z: 0.5); color: (r: 0.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: -0.5; y: 0.5; z: 0.5); color: (r: 1.0; g: 1.0; b: 0.0; a: 1.0)),

            // top face
            (pos: (x: -0.5; y: 0.5; z: -0.5); color: (r: 1.0; g: 0.0; b: 0.0; a: 1.0)),
            (pos: (x: 0.5; y: 0.5; z: 0.5); color: (r: 1.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: 0.5; y: 0.5; z: -0.5); color: (r: 0.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: -0.5; y: 0.5; z: 0.5); color: (r: 1.0; g: 1.0; b: 0.0; a: 1.0)),

            // bottom face
            (pos: (x: 0.5; y: -0.5; z: 0.5); color: (r: 1.0; g: 0.0; b: 0.0; a: 1.0)),
            (pos: (x: -0.5; y: -0.5; z: -0.5); color: (r: 1.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: 0.5; y: -0.5; z: -0.5); color: (r: 0.0; g: 0.0; b: 1.0; a: 1.0)),
            (pos: (x: -0.5; y: -0.5; z: 0.5); color: (r: 1.0; g: 1.0; b: 0.0; a: 1.0))

            );

    var
        hr: HRESULT;
        dxgiFactory: IDXGIFactory4;
        adapter: IDXGIAdapter1; // adapters are the graphics card (this includes the embedded graphics on the motherboard)
        WarpAdapter: IDXGIAdapter;
        adapterIndex: integer = 0; // we'll start looking for directx 12  compatible graphics devices starting at index 0
        adapterFound: boolean = False; // set this to true when a good one was found
        desc: TDXGI_ADAPTER_DESC1;
        cqDesc: TD3D12_COMMAND_QUEUE_DESC;
        backBufferDesc: TDXGI_MODE_DESC; // this is to describe our display mode
        sampleDesc: TDXGI_SAMPLE_DESC;
        swapChainDesc: TDXGI_SWAP_CHAIN_DESC;
        tempSwapChain: IDXGISwapChain;
        rtvHeapDesc: TD3D12_DESCRIPTOR_HEAP_DESC;
        rtvHandle: TD3D12_CPU_DESCRIPTOR_HANDLE;
        i: integer;

        rootSignatureDesc: TD3DX12_ROOT_SIGNATURE_DESC;
        signature: ID3DBlob;

        vertexShader: ID3DBlob; // d3d blob for holding vertex shader bytecode
        errorBuff: ID3DBlob; // a buffer holding the error data if any
        vertexShaderBytecode: TD3D12_SHADER_BYTECODE;
        pixelShader: ID3DBlob;
        pixelShaderBytecode: TD3D12_SHADER_BYTECODE;

        inputLayoutDesc: TD3D12_INPUT_LAYOUT_DESC;
        psoDesc: TD3D12_GRAPHICS_PIPELINE_STATE_DESC; // a structure to define a pso

        vBufferSize: integer;
        vBufferUploadHeap: ID3D12Resource;
        vertexData: TD3D12_SUBRESOURCE_DATA;
        ppCommandLists: array [0..0] of ID3D12CommandList;

        debugController: ID3D12Debug;

        lUseWarpDevice: boolean = False;
        lUseDebug: boolean = False;
        debugdevice: ID3D12DebugDevice;

        dxgiFactoryFlags: UINT = 0;
        iTemp: UINT;
        lltemp: TD3DX12_RESOURCE_BARRIER;

        // a quad (2 triangles)

        iList: array [0..35] of DWORD = (
            // ffront face
            0, 1, 2, // first triangle
            0, 3, 1, // second triangle

            // left face
            4, 5, 6, // first triangle
            4, 7, 5, // second triangle

            // right face
            8, 9, 10, // first triangle
            8, 11, 9, // second triangle

            // back face
            12, 13, 14, // first triangle
            12, 15, 13, // second triangle

            // top face
            16, 17, 18, // first triangle
            16, 19, 17, // second triangle

            // bottom face
            20, 21, 22, // first triangle
            20, 23, 21 // second triangle
            );

        iBufferSize: integer;
        iBufferUploadHeap: ID3D12Resource;
        indexData: TD3D12_SUBRESOURCE_DATA;

        dsvHeapDesc: TD3D12_DESCRIPTOR_HEAP_DESC;
        depthStencilDesc: TD3D12_DEPTH_STENCIL_VIEW_DESC;
        depthOptimizedClearValue: TD3D12_CLEAR_VALUE;
        dsh: TD3D12_CPU_DESCRIPTOR_HANDLE;
        hp: TD3D12_HEAP_PROPERTIES;
        rd: TD3DX12_RESOURCE_DESC;

        descriptorTableRanges: array[0..0] of TD3D12_DESCRIPTOR_RANGE; // only one range right now
        descriptorTable: TD3D12_ROOT_DESCRIPTOR_TABLE;
        rootParameters: array[0..0] of TD3D12_ROOT_PARAMETER; // only one parameter right now
        heapDesc: TD3D12_DESCRIPTOR_HEAP_DESC;

        cbvDesc: TD3D12_CONSTANT_BUFFER_VIEW_DESC;
        readRange: TD3DX12_RANGE;
        lrv: TD3D12_CPU_DESCRIPTOR_HANDLE;

        rootCBVDescriptor: TD3D12_ROOT_DESCRIPTOR;

        test: pointer = nil;

        tmpMat: TXMMATRIX;
        cPos, cTarg, cUp: TXMVECTOR;
        posVec: TXMVECTOR;
    begin
        Result := False;

        ConstantBufferPerObjectAlignedSize := (sizeof(TConstantBufferPerObject) + 255) and not 255;

        if lUseDebug then
        begin
            hr := D3D12GetDebugInterface(IID_ID3D12Debug, debugController);
            debugController.EnableDebugLayer();
            dxgiFactoryFlags := dxgiFactoryFlags or DXGI_CREATE_FACTORY_DEBUG;
        end;

        // -- Create the Device -- //
        // hr := CreateDXGIFactory2(dxgiFactoryFlags, IID_IDXGIFactory4, dxgiFactory);
        hr := CreateDXGIFactory1(IID_IDXGIFactory4, dxgiFactory);
        if (hr = S_OK) then
        begin
            if lUseDebug then
            begin
                hr := dxgiFactory.EnumWarpAdapter(IID_IDXGIAdapter, warpAdapter);

                adapterFound := True;
                hr := D3D12CreateDevice(warpAdapter, D3D_FEATURE_LEVEL_11_0, IID_ID3D12Device, @device);
                outputDebugStringA(pAnsichar(IntToHex(hr, 8)));
            end
            else
            begin
                // find first hardware gpu that supports d3d 12
                while (dxgiFactory.EnumAdapters1(adapterIndex, adapter) <> DXGI_ERROR_NOT_FOUND) do
                begin
                    adapter.GetDesc1(desc);

                    if (desc.Flags and Ord(DXGI_ADAPTER_FLAG_SOFTWARE) = Ord(DXGI_ADAPTER_FLAG_SOFTWARE)) then
                    begin
                        // we dont want a software device
                        Inc(adapterIndex);
                        continue;
                    end;
                    // we want a device that is compatible with direct3d 12 (feature level 11 or higher)
                    hr := D3D12CreateDevice(adapter, D3D_FEATURE_LEVEL_11_0, IID_ID3D12Device, nil);
                    if (SUCCEEDED(hr)) then
                    begin
                        adapterFound := True;
                        break;
                    end;

                    Inc(adapterIndex);
                end;

                if (not adapterFound) then
                    Exit;
            end;

            if (SUCCEEDED(hr)) then
            begin
                // Create the device
                hr := D3D12CreateDevice(adapter, D3D_FEATURE_LEVEL_11_0, IID_ID3D12Device, @device);
                outputDebugStringA(pAnsichar(IntToHex(hr, 8)));
            end;
        end;


        // -- Create a direct command queue -- //
        if (SUCCEEDED(hr)) then
        begin
            cqDesc.Flags := D3D12_COMMAND_QUEUE_FLAG_NONE;
            cqDesc._Type := D3D12_COMMAND_LIST_TYPE_DIRECT; // direct means the gpu can directly execute this command queue

            hr := device.CreateCommandQueue(cqDesc, IID_ID3D12CommandQueue, commandQueue); // create the command queue
            outputDebugStringA(pAnsichar(IntToHex(hr, 8)));
        end;
        if (hr <> S_OK) then
            Exit;

        // -- Create the Swap Chain (double/tripple buffering) -- //
        backBufferDesc.Width := Width; // buffer width
        backBufferDesc.Height := Height; // buffer height
        backBufferDesc.Format := DXGI_FORMAT_R8G8B8A8_UNORM; // format of the buffer (rgba 32 bits, 8 bits for each chanel)

        // describe our multi-sampling. We are not multi-sampling, so we set the count to 1 (we need at least one sample of course)
        sampleDesc.Count := 1; // multisample count (no multisampling, so we just put 1, since we still need 1 sample)

        // Describe and create the swap chain.
        swapChainDesc.BufferCount := frameBufferCount; // number of buffers we have
        swapChainDesc.BufferDesc := backBufferDesc; // our back buffer description
        swapChainDesc.BufferUsage := DXGI_USAGE_RENDER_TARGET_OUTPUT; // this says the pipeline will render to this swap chain
        swapChainDesc.SwapEffect := DXGI_SWAP_EFFECT_FLIP_DISCARD; // dxgi will discard the buffer (data) after we call present
        swapChainDesc.OutputWindow := Handle; // handle to our window
        swapChainDesc.SampleDesc := sampleDesc; // our multi-sampling description
        swapChainDesc.Windowed := not FullScreen;
        // set to true, then if in fullscreen must call SetFullScreenState with true for full screen to get uncapped fps

        hr := dxgiFactory.CreateSwapChain(commandQueue, // the queue will be flushed once the swap chain is created
            @swapChainDesc, // give it the swap chain description we created above
            tempSwapChain // store the created swap chain in a temp IDXGISwapChain interface
            );
        outputDebugStringA(pAnsichar(IntToHex(hr, 8)));

        swapChain := IDXGISwapChain3(tempSwapChain);
        frameIndex := swapChain.GetCurrentBackBufferIndex();

        // -- Create the Back Buffers (render target views) Descriptor Heap -- //

        // describe an rtv descriptor heap and create

        rtvHeapDesc.NumDescriptors := frameBufferCount; // number of descriptors for this heap.
        rtvHeapDesc._Type := D3D12_DESCRIPTOR_HEAP_TYPE_RTV; // this heap is a render target view heap

        // This heap will not be directly referenced by the shaders (not shader visible), as this will store the output from the pipeline
        // otherwise we would set the heap's flag to D3D12_DESCRIPTOR_HEAP_FLAG_SHADER_VISIBLE
        rtvHeapDesc.Flags := D3D12_DESCRIPTOR_HEAP_FLAG_NONE;
        hr := device.CreateDescriptorHeap(@rtvHeapDesc, IID_ID3D12DescriptorHeap, @rtvDescriptorHeap);
        if (hr <> S_OK) then
            Exit;




        // get the size of a descriptor in this heap (this is a rtv heap, so only rtv descriptors should be stored in it.
        // descriptor sizes may vary from device to device, which is why there is no set size and we must ask the
        // device to give us the size. we will use this size to increment a descriptor handle offset
        rtvDescriptorSize := device.GetDescriptorHandleIncrementSize(D3D12_DESCRIPTOR_HEAP_TYPE_RTV);

        // get a handle to the first descriptor in the descriptor heap. a handle is basically a pointer,
        // but we cannot literally use it like a c++ pointer.

        rtvDescriptorHeap.GetCPUDescriptorHandleForHeapStart(rtvHandle);

        // Create a RTV for each buffer (double buffering is two buffers, tripple buffering is 3).
        for i := 0 to frameBufferCount - 1 do
        begin
            // first we get the n'th buffer in the swap chain and store it in the n'th
            // position of our ID3D12Resource array
            hr := swapChain.GetBuffer(i, IID_ID3D12Resource, renderTargets[i]);
            if (hr <> S_OK) then
                Exit;

            // the we "create" a render target view which binds the swap chain buffer (ID3D12Resource[n]) to the rtv handle
            device.CreateRenderTargetView(renderTargets[i], nil, rtvHandle);

            // we increment the rtv handle by the rtv descriptor size we got above
            rtvHandle.Offset(1, rtvDescriptorSize);
        end;

        // -- Create the Command Allocators -- //
        for i := 0 to frameBufferCount - 1 do
        begin
            hr := device.CreateCommandAllocator(D3D12_COMMAND_LIST_TYPE_DIRECT, IID_ID3D12CommandAllocator, commandAllocator[i]);
            if (hr <> S_OK) then
                Exit;
        end;

        // -- Create a Command List -- //

        // create the command list with the first allocator
        hr := device.CreateCommandList(0, D3D12_COMMAND_LIST_TYPE_DIRECT, commandAllocator[frameIndex], nil,
            IID_ID3D12GraphicsCommandList, commandList);
        if (hr <> S_OK) then
            Exit;

        // -- Create a Fence & Fence Event -- //

        // create the fences
        for i := 0 to frameBufferCount - 1 do
        begin
            hr := device.CreateFence(0, D3D12_FENCE_FLAG_NONE, IID_ID3D12Fence, fence[i]);
            if (hr <> S_OK) then
                Exit;
            fenceValue[i] := 0; // set the initial fence value to 0
        end;

        // create a handle to a fence event
        fenceEvent := CreateEvent(0, False, False, nil);
        if (fenceEvent = 0) then
            Exit;


        // create root signature

        // create a descriptor range (descriptor table) and fill it out
        // this is a range of descriptors inside a descriptor heap

        // create a root descriptor, which explains where to find the data for this root parameter

        rootCBVDescriptor.RegisterSpace := 0;
        rootCBVDescriptor.ShaderRegister := 0;

        // create a root parameter and fill it out

        rootParameters[0].ParameterType := D3D12_ROOT_PARAMETER_TYPE_CBV; // this is a constant buffer view root descriptor
        rootParameters[0].Descriptor := rootCBVDescriptor; // this is the root descriptor for this root parameter
        rootParameters[0].ShaderVisibility := D3D12_SHADER_VISIBILITY_VERTEX;
        // our pixel shader will be the only shader accessing this parameter for now


        rootSignatureDesc.Init(Length(rootParameters), // we have 1 root parameter
            @rootParameters[0], // a pointer to the beginning of our root parameters array
            0,
            nil,
            TD3D12_ROOT_SIGNATURE_FLAGS(Ord(D3D12_ROOT_SIGNATURE_FLAG_ALLOW_INPUT_ASSEMBLER_INPUT_LAYOUT) or
            // we can deny shader stages here for better performance
            Ord(D3D12_ROOT_SIGNATURE_FLAG_DENY_HULL_SHADER_ROOT_ACCESS) or Ord(D3D12_ROOT_SIGNATURE_FLAG_DENY_DOMAIN_SHADER_ROOT_ACCESS) or
            Ord(D3D12_ROOT_SIGNATURE_FLAG_DENY_GEOMETRY_SHADER_ROOT_ACCESS) or Ord(D3D12_ROOT_SIGNATURE_FLAG_DENY_PIXEL_SHADER_ROOT_ACCESS)));


        hr := D3D12SerializeRootSignature(rootSignatureDesc, D3D_ROOT_SIGNATURE_VERSION_1, signature, nil);
        if (FAILED(hr)) then
            Exit;

        hr := device.CreateRootSignature(0, signature.GetBufferPointer(), signature.GetBufferSize(), IID_ID3D12RootSignature, rootSignature);
        if (FAILED(hr)) then
            Exit;

        // create vertex and pixel shaders

        // when debugging, we can compile the shader files at runtime.
        // but for release versions, we can compile the hlsl shaders
        // with fxc.exe to create .cso files, which contain the shader
        // bytecode. We can load the .cso files at runtime to get the
        // shader bytecode, which of course is faster than compiling
        // them at runtime

        // compile vertex shader


        hr := D3DCompileFromFile('VertexShader.hlsl', nil, nil, 'main', 'vs_5_0', D3DCOMPILE_DEBUG or
            D3DCOMPILE_SKIP_OPTIMIZATION, 0, vertexShader, errorBuff);
        if (FAILED(hr)) then
        begin
            OutputDebugStringA(PAnsiChar(errorBuff.GetBufferPointer()));
            exit;
        end;

        // fill out a shader bytecode structure, which is basically just a pointer
        // to the shader bytecode and the size of the shader bytecode


        vertexShaderBytecode.BytecodeLength := vertexShader.GetBufferSize();
        vertexShaderBytecode.pShaderBytecode := vertexShader.GetBufferPointer();

        // compile pixel shader

        hr := D3DCompileFromFile('PixelShader.hlsl', nil, nil, 'main', 'ps_5_0', D3DCOMPILE_DEBUG or D3DCOMPILE_SKIP_OPTIMIZATION,
            0, pixelShader, errorBuff);
        if (FAILED(hr)) then
        begin
            OutputDebugStringA(pAnsiChar(errorBuff.GetBufferPointer()));
            Exit;
        end;

        // fill out shader bytecode structure for pixel shader

        pixelShaderBytecode.BytecodeLength := pixelShader.GetBufferSize();
        pixelShaderBytecode.pShaderBytecode := pixelShader.GetBufferPointer();

        // create input layout

        // The input layout is used by the Input Assembler so that it knows
        // how to read the vertex data bound to it.



        // fill out an input layout description structure


        // we can get the number of elements in an array by "sizeof(array) / sizeof(arrayElementType)"
        inputLayoutDesc.NumElements := sizeof(inputLayout) div sizeof(TD3D12_INPUT_ELEMENT_DESC);
        inputLayoutDesc.pInputElementDescs := @inputLayout[0];

        // create a pipeline state object (PSO)

        // In a real application, you will have many pso's. for each different shader
        // or different combinations of shaders, different blend states or different rasterizer states,
        // different topology types (point, line, triangle, patch), or a different number
        // of render targets you will need a pso

        // VS is the only required shader for a pso. You might be wondering when a case would be where
        // you only set the VS. It's possible that you have a pso that only outputs data with the stream
        // output, and not on a render target, which means you would not need anything after the stream
        // output.


        psoDesc.InputLayout := inputLayoutDesc; // the structure describing our input layout
        psoDesc.pRootSignature := rootSignature; // the root signature that describes the input data this pso needs
        psoDesc.VS := vertexShaderBytecode; // structure describing where to find the vertex shader bytecode and how large it is
        psoDesc.PS := pixelShaderBytecode; // same as VS but for pixel shader
        psoDesc.PrimitiveTopologyType := D3D12_PRIMITIVE_TOPOLOGY_TYPE_TRIANGLE; // type of topology we are drawing
        psoDesc.RTVFormats[0] := DXGI_FORMAT_R8G8B8A8_UNORM; // format of the render target
        psoDesc.SampleDesc := sampleDesc; // must be the same sample description as the swapchain and depth/stencil buffer
        psoDesc.SampleMask := $ffffffff; // sample mask has to do with multi-sampling. 0xffffffff means point sampling is done
        psoDesc.RasterizerState.Create(D3D12_DEFAULT); // a default rasterizer state.
        psoDesc.BlendState.Create(D3D12_DEFAULT);  // a default blent state.
        psoDesc.NumRenderTargets := 1; // we are only binding one render target
        psoDesc.DepthStencilState := TD3DX12_DEPTH_STENCIL_DESC.Create(D3D12_DEFAULT); // a default depth stencil state

        // create the pso
        hr := device.CreateGraphicsPipelineState(@psoDesc, IID_ID3D12PipelineState, pipelineStateObject);
        if (FAILED(hr)) then
            Exit;

        // Create vertex buffer

        vBufferSize := sizeof(vList);

        // create default heap
        // default heap is memory on the GPU. Only the GPU has access to this memory
        // To get data into this heap, we will have to upload the data using
        // an upload heap
        hp := TD3DX12_HEAP_PROPERTIES.Create(D3D12_HEAP_TYPE_DEFAULT);
        rd := TD3DX12_RESOURCE_DESC.Buffer(vBufferSize);
        hr := device.CreateCommittedResource(@hp, // a default heap
            D3D12_HEAP_FLAG_NONE, // no flags
            @rd, // resource description for a buffer
            D3D12_RESOURCE_STATE_COPY_DEST, // we will start this heap in the copy destination state since we will copy data
            // from the upload heap to this heap
            nil, // optimized clear value must be null for this type of resource. used for render targets and depth/stencil buffers
            IID_ID3D12Resource, vertexBuffer);

        // we can give resource heaps a name so when we debug with the graphics debugger we know what resource we are looking at
        vertexBuffer.SetName('Vertex Buffer Resource Heap');

        // create upload heap
        // upload heaps are used to upload data to the GPU. CPU can write to it, GPU can read from it
        // We will upload the vertex buffer using this heap to the default heap
        hp := TD3DX12_HEAP_PROPERTIES.Create(D3D12_HEAP_TYPE_UPLOAD);
        rd := TD3DX12_RESOURCE_DESC.Buffer(vBufferSize);
        hr := device.CreateCommittedResource(@hp, // upload heap
            D3D12_HEAP_FLAG_NONE, // no flags
            @rd, // resource description for a buffer
            D3D12_RESOURCE_STATE_GENERIC_READ, // GPU will read from this buffer and copy its contents to the default heap
            nil, IID_ID3D12Resource, vBufferUploadHeap);
        vBufferUploadHeap.SetName('Vertex Buffer Upload Resource Heap');

        // store vertex buffer in upload heap

        vertexData.pData := @vList[0]; // pointer to our vertex array
        vertexData.RowPitch := vBufferSize; // size of all our triangle vertex data
        vertexData.SlicePitch := vBufferSize; // also the size of our triangle vertex data


        // we are now creating a command with the command list to copy the data from
        // the upload heap to the default heap
        UpdateSubresources(commandList, vertexBuffer, vBufferUploadHeap, 0, 0, 1, @vertexData);

        // transition the vertex buffer data from copy destination state to vertex buffer state
        lltemp := TD3DX12_RESOURCE_BARRIER.CreateTransition(vertexBuffer, D3D12_RESOURCE_STATE_COPY_DEST,
            D3D12_RESOURCE_STATE_VERTEX_AND_CONSTANT_BUFFER);
        commandList.ResourceBarrier(1, @lltemp);


        // Create index buffer


        iBufferSize := sizeof(iList);

        numCubeIndices := sizeof(iList) div sizeof(DWORD);

        // create default heap to hold index buffer
        hp := TD3DX12_HEAP_PROPERTIES.Create(D3D12_HEAP_TYPE_DEFAULT);
        rd := TD3DX12_RESOURCE_DESC.Buffer(iBufferSize);
        device.CreateCommittedResource(@hp, // a default heap
            D3D12_HEAP_FLAG_NONE, // no flags
            @rd, // resource description for a buffer
            D3D12_RESOURCE_STATE_COPY_DEST, // start in the copy destination state
            nil, // optimized clear value must be null for this type of resource
            IID_ID3D12Resource, indexBuffer);

        // we can give resource heaps a name so when we debug with the graphics debugger we know what resource we are looking at
        vertexBuffer.SetName('Index Buffer Resource Heap');

        // create upload heap to upload index buffer
        hp := TD3DX12_HEAP_PROPERTIES.Create(D3D12_HEAP_TYPE_UPLOAD);
        rd := TD3DX12_RESOURCE_DESC.Buffer(vBufferSize);
        device.CreateCommittedResource(@hp, // upload heap
            D3D12_HEAP_FLAG_NONE, // no flags
            @rd, // resource description for a buffer
            D3D12_RESOURCE_STATE_GENERIC_READ, // GPU will read from this buffer and copy its contents to the default heap
            nil,
            IID_ID3D12Resource, iBufferUploadHeap);
        vBufferUploadHeap.SetName('Index Buffer Upload Resource Heap');

        // store vertex buffer in upload heap


        indexData.pData := @iList[0]; // pointer to our index array
        indexData.RowPitch := iBufferSize; // size of all our index buffer
        indexData.SlicePitch := iBufferSize; // also the size of our index buffer

        // we are now creating a command with the command list to copy the data from
        // the upload heap to the default heap
        UpdateSubresources(commandList, indexBuffer, iBufferUploadHeap, 0, 0, 1, @indexData);

        // transition the vertex buffer data from copy destination state to vertex buffer state
        lltemp := TD3DX12_RESOURCE_BARRIER.CreateTransition(indexBuffer, D3D12_RESOURCE_STATE_COPY_DEST,
            D3D12_RESOURCE_STATE_VERTEX_AND_CONSTANT_BUFFER);
        commandList.ResourceBarrier(1, @lltemp);

        // Create the depth/stencil buffer

        // create a depth stencil descriptor heap so we can get a pointer to the depth stencil buffer

        dsvHeapDesc.NumDescriptors := 1;
        dsvHeapDesc._Type := D3D12_DESCRIPTOR_HEAP_TYPE_DSV;
        dsvHeapDesc.Flags := D3D12_DESCRIPTOR_HEAP_FLAG_NONE;
        hr := device.CreateDescriptorHeap(@dsvHeapDesc, IID_ID3D12DescriptorHeap, nil);
        hr := device.CreateDescriptorHeap(@dsvHeapDesc, IID_ID3D12DescriptorHeap, @dsDescriptorHeap);
        if (FAILED(hr)) then
        begin
            Running := False;
        end;


        depthStencilDesc.Format := DXGI_FORMAT_D32_FLOAT;
        depthStencilDesc.ViewDimension := D3D12_DSV_DIMENSION_TEXTURE2D;
        depthStencilDesc.Flags := D3D12_DSV_FLAG_NONE;


        depthOptimizedClearValue.Format := DXGI_FORMAT_D32_FLOAT;
        depthOptimizedClearValue.DepthStencil.Depth := 1.0;
        depthOptimizedClearValue.DepthStencil.Stencil := 0;

        hp := TD3DX12_HEAP_PROPERTIES.Create(D3D12_HEAP_TYPE_DEFAULT);
        rd := TD3DX12_RESOURCE_DESC.Tex2D(DXGI_FORMAT_D32_FLOAT, Width, Height, 1, 0, 1, 0, D3D12_RESOURCE_FLAG_ALLOW_DEPTH_STENCIL);
        hr := device.CreateCommittedResource(@hp, D3D12_HEAP_FLAG_NONE, @rd, D3D12_RESOURCE_STATE_DEPTH_WRITE,
            @depthOptimizedClearValue, IID_ID3D12Resource, depthStencilBuffer);
        hr := dsDescriptorHeap.SetName('Depth/Stencil Resource Heap');


        dsDescriptorHeap.GetCPUDescriptorHandleForHeapStart(dsh);
        device.CreateDepthStencilView(depthStencilBuffer, depthStencilDesc, dsh);


        // create the constant buffer resource heap
        // We will update the constant buffer one or more times per frame, so we will use only an upload heap
        // unlike previously we used an upload heap to upload the vertex and index data, and then copied over
        // to a default heap. If you plan to use a resource for more than a couple frames, it is usually more
        // efficient to copy to a default heap where it stays on the gpu. In this case, our constant buffer
        // will be modified and uploaded at least once per frame, so we only use an upload heap

        // first we will create a resource heap (upload heap) for each frame for the cubes constant buffers
        // As you can see, we are allocating 64KB for each resource we create. Buffer resource heaps must be
        // an alignment of 64KB. We are creating 3 resources, one for each frame. Each constant buffer is
        // only a 4x4 matrix of floats in this tutorial. So with a float being 4 bytes, we have
        // 16 floats in one constant buffer, and we will store 2 constant buffers in each
        // heap, one for each cube, thats only 64x2 bits, or 128 bits we are using for each
        // resource, and each resource must be at least 64KB (65536 bits)

        rd := TD3DX12_RESOURCE_DESC.Buffer(1024 * 64);
        hp := TD3DX12_HEAP_PROPERTIES.Create(D3D12_HEAP_TYPE_UPLOAD);


        for  i := 0 to frameBufferCount - 1 do
        begin
            // create resource for cube 1
            hr := device.CreateCommittedResource(TD3DX12_HEAP_PROPERTIES.Create(D3D12_HEAP_TYPE_UPLOAD),
                // this heap will be used to upload the constant buffer data
                D3D12_HEAP_FLAG_NONE, // no flags
                TD3DX12_RESOURCE_DESC.Buffer(1024 * 64),
                // size of the resource heap. Must be a multiple of 64KB for single-textures and constant buffers
                D3D12_RESOURCE_STATE_GENERIC_READ, // will be data that is read from so we keep it in the generic read state
                nil, // we do not have use an optimized clear value for constant buffers
                IID_ID3D12Resource, constantBufferUploadHeaps[i]);
            constantBufferUploadHeaps[i].SetName('Constant Buffer Upload Resource Heap');

            ZeroMemory(@cbPerObject, sizeof(cbPerObject));

            readRange := TD3DX12_RANGE.Create(0, 0);
            // We do not intend to read from this resource on the CPU. (so end is less than or equal to begin)

            // map the resource heap to get a gpu virtual address to the beginning of the heap
            hr := constantBufferUploadHeaps[i].Map(0, @readRange, cbvGPUAddress[i]);

            // Because of the constant read alignment requirements, constant buffer views must be 256 bit aligned. Our buffers are smaller than 256 bits,
            // so we need to add spacing between the two buffers, so that the second buffer starts at 256 bits from the beginning of the resource heap.
            Windows.CopyMemory(cbvGPUAddress[i], @cbPerObject, sizeof(cbPerObject)); // cube1's constant buffer data
            Windows.CopyMemory(cbvGPUAddress[i] + ConstantBufferPerObjectAlignedSize, @cbPerObject, sizeof(cbPerObject));
            // cube2's constant buffer data

        end;



        // Now we execute the command list to upload the initial assets (triangle data)
        commandList.Close();


        ppCommandLists[0] := commandList;
        commandQueue.ExecuteCommandLists(Length(ppCommandLists), @ppCommandLists[0]);
        Sleep(1); // Why that BULLSHIT, without this sleep no triangle is shown, the C++ don't need a sleep here ?!?
        // increment the fence value now, otherwise the buffer might not be uploaded by the time we start drawing
        Inc(fenceValue[frameIndex]);
        hr := commandQueue.Signal(fence[frameIndex], fenceValue[frameIndex]);
        if (FAILED(hr)) then
            Exit;

        // create a vertex buffer view for the triangle. We get the GPU memory address to the vertex pointer using the GetGPUVirtualAddress() method
        vertexBufferView.BufferLocation := vertexBuffer.GetGPUVirtualAddress();
        vertexBufferView.StrideInBytes := sizeof(TVertex);
        vertexBufferView.SizeInBytes := vBufferSize;

        // create a vertex buffer view for the triangle. We get the GPU memory address to the vertex pointer using the GetGPUVirtualAddress() method
        indexBufferView.BufferLocation := indexBuffer.GetGPUVirtualAddress();
        indexBufferView.Format := DXGI_FORMAT_R32_UINT; // 32-bit unsigned integer (this is what a dword is, double word, a word is 2 bytes)
        indexBufferView.SizeInBytes := iBufferSize;


        // Fill out the Viewport
        viewport.TopLeftX := 0;
        viewport.TopLeftY := 0;
        viewport.Width := Width;
        viewport.Height := Height;
        viewport.MinDepth := 0.0;
        viewport.MaxDepth := 1.0;

        // Fill out a scissor rect
        scissorRect.left := 0;
        scissorRect.top := 0;
        scissorRect.right := Width;
        scissorRect.bottom := Height;



        // build projection and view matrix
        tmpMat := XMMatrixPerspectiveFovLH(45.0 * (3.14 / 180.0), Width / Height, 0.1, 1000.0);
        XMStoreFloat4x4(cameraProjMat, tmpMat);

        // set starting camera state
        cameraPosition := TXMFLOAT4.Create(0.0, 2.0, -4.0, 0.0);
        cameraTarget := TXMFLOAT4.Create(0.0, 0.0, 0.0, 0.0);
        cameraUp := TXMFLOAT4.Create(0.0, 1.0, 0.0, 0.0);

        // build view matrix

        cPos := XMLoadFloat4(cameraPosition);
        cTarg := XMLoadFloat4(cameraTarget);
        cUp := XMLoadFloat4(cameraUp);
        tmpMat := XMMatrixLookAtLH(cPos, cTarg, cUp);
        XMStoreFloat4x4(cameraViewMat, tmpMat);

        // set starting cubes position
        // first cube

        cube1Position := TXMFLOAT4.Create(0.0, 0.0, 0.0, 0.0); // set cube 1's position
        posVec := XMLoadFloat4(cube1Position); // create xmvector for cube1's position

        tmpMat := XMMatrixTranslationFromVector(posVec); // create translation matrix from cube1's position vector
        XMStoreFloat4x4(cube1RotMat, XMMatrixIdentity()); // initialize cube1's rotation matrix to identity matrix
        XMStoreFloat4x4(cube1WorldMat, tmpMat); // store cube1's world matrix

        // second cube
        cube2PositionOffset := TXMFLOAT4.Create(1.5, 0.0, 0.0, 0.0);
        posVec := XMLoadFloat4(cube2PositionOffset) + XMLoadFloat4(cube1Position); // create xmvector for cube2's position
        // we are rotating around cube1 here, so add cube2's position to cube1

        tmpMat := XMMatrixTranslationFromVector(posVec); // create translation matrix from cube2's position offset vector
        XMStoreFloat4x4(cube2RotMat, XMMatrixIdentity()); // initialize cube2's rotation matrix to identity matrix
        XMStoreFloat4x4(cube2WorldMat, tmpMat); // store cube2's world matrix



        Result := True;
    end;



    procedure Update();
    var
        rotXMat, rotYMat, rotZMat: TXMMATRIX;
        rotMat: TXMMATRIX;
        translationMat: TXMMATRIX;
        worldMat: TXMMATRIX;

        viewMat, projMat, wvpMat, transposed, translationOffsetMat: TXMMATRIX;
        scaleMat: TXMMATRIX;
    begin
        // create rotation matrices
        rotXMat := XMMatrixRotationX(0.0001);
        rotYMat := XMMatrixRotationY(0.0002);
        rotZMat := XMMatrixRotationZ(0.0003);

        // add rotation to cube1's rotation matrix and store it
        rotMat := XMLoadFloat4x4(cube1RotMat) * rotXMat * rotYMat * rotZMat;
        XMStoreFloat4x4(cube1RotMat, rotMat);

        // create translation matrix for cube 1 from cube 1's position vector
        translationMat := XMMatrixTranslationFromVector(XMLoadFloat4(cube1Position));

        // create cube1's world matrix by first rotating the cube, then positioning the rotated cube
        worldMat := rotMat * translationMat;

        // store cube1's world matrix
        XMStoreFloat4x4(cube1WorldMat, worldMat);

        // update constant buffer for cube1
        // create the wvp matrix and store in constant buffer
        viewMat := XMLoadFloat4x4(cameraViewMat); // load view matrix
        projMat := XMLoadFloat4x4(cameraProjMat); // load projection matrix
        wvpMat := XMLoadFloat4x4(cube1WorldMat) * viewMat * projMat; // create wvp matrix
        transposed := XMMatrixTranspose(wvpMat); // must transpose wvp matrix for the gpu
        XMStoreFloat4x4(cbPerObject.wvpMat, transposed); // store transposed wvp matrix in constant buffer

        // copy our ConstantBuffer instance to the mapped constant buffer resource
        Windows.CopyMemory(cbvGPUAddress[frameIndex], @cbPerObject, sizeof(cbPerObject));

        // now do cube2's world matrix
        // create rotation matrices for cube2
        rotXMat := XMMatrixRotationX(0.0003);
        rotYMat := XMMatrixRotationY(0.0002);
        rotZMat := XMMatrixRotationZ(0.0001);

        // add rotation to cube2's rotation matrix and store it
        rotMat := rotZMat * (XMLoadFloat4x4(cube2RotMat) * (rotXMat * rotYMat));
        XMStoreFloat4x4(cube2RotMat, rotMat);

        // create translation matrix for cube 2 to offset it from cube 1 (its position relative to cube1
        translationOffsetMat := XMMatrixTranslationFromVector(XMLoadFloat4(cube2PositionOffset));

        // we want cube 2 to be half the size of cube 1, so we scale it by .5 in all dimensions
        scaleMat := XMMatrixScaling(0.5, 0.5, 0.5);

        // reuse worldMat.
        // first we scale cube2. scaling happens relative to point 0,0,0, so you will almost always want to scale first
        // then we translate it.
        // then we rotate it. rotation always rotates around point 0,0,0
        // finally we move it to cube 1's position, which will cause it to rotate around cube 1
        worldMat := scaleMat * translationOffsetMat * rotMat * translationMat;

        wvpMat := XMLoadFloat4x4(cube2WorldMat) * viewMat * projMat; // create wvp matrix
        transposed := XMMatrixTranspose(wvpMat); // must transpose wvp matrix for the gpu
        XMStoreFloat4x4(cbPerObject.wvpMat, transposed); // store transposed wvp matrix in constant buffer

        // copy our ConstantBuffer instance to the mapped constant buffer resource
        Windows.CopyMemory(cbvGPUAddress[frameIndex] + ConstantBufferPerObjectAlignedSize, @cbPerObject, sizeof(cbPerObject));

        // store cube2's world matrix
        XMStoreFloat4x4(cube2WorldMat, worldMat);
    end;



    procedure WaitForPreviousFrame();
    var
        hr: HResult;
        dw: dword;
    begin
        // swap the current rtv buffer index so we draw on the correct buffer
        frameIndex := swapChain.GetCurrentBackBufferIndex();

        // if the current fence value is still less than "fenceValue", then we know the GPU has not finished executing
        // the command queue since it has not reached the "commandQueue.Signal(fence, fenceValue)" command
        if (fence[frameIndex].GetCompletedValue() < fenceValue[frameIndex]) then
        begin
            outputDebugStringA(pansichar('Fence: ' + IntToStr(fenceValue[frameIndex])));
            // we have the fence create an event which is signaled once the fence's current value is "fenceValue"
            hr := fence[frameIndex].SetEventOnCompletion(fenceValue[frameIndex], fenceEvent);
            if (hr <> S_OK) then
            begin
                Running := False;
            end;

            // We will wait until the fence has triggered the event that it's current value has reached "fenceValue". once it's value
            // has reached "fenceValue", we know the command queue has finished executing
            dw := WaitForSingleObject(fenceEvent, INFINITE);
        end;

        // increment fenceValue for next frame
        Inc(fenceValue[frameIndex]);
    end;



    procedure UpdatePipeline();
    const
        clearColor: array[0..3] of single = (0.0, 0.2, 0.4, 1.0);
    var
        hr: Hresult;
        rtvHandle: TD3D12_CPU_DESCRIPTOR_HANDLE;
        ltest: TD3DX12_RESOURCE_BARRIER;
        dsvHandle: TD3D12_CPU_DESCRIPTOR_HANDLE;
        descriptorHeaps: array[0..0] of PID3D12DescriptorHeap;
    begin

        // We have to wait for the gpu to finish with the command allocator before we reset it
        WaitForPreviousFrame();

        // we can only reset an allocator once the gpu is done with it
        // resetting an allocator frees the memory that the command list was stored in
        hr := commandAllocator[frameIndex].Reset();
        if (hr <> S_OK) then
        begin
            Running := False;
        end;

        // reset the command list. by resetting the command list we are putting it into
        // a recording state so we can start recording commands into the command allocator.
        // the command allocator that we reference here may have multiple command lists
        // associated with it, but only one can be recording at any time. Make sure
        // that any other command lists associated to this command allocator are in
        // the closed state (not recording).
        // Here you will pass an initial pipeline state object as the second parameter,
        // but in this tutorial we are only clearing the rtv, and do not actually need
        // anything but an initial default pipeline, which is what we get by setting
        // the second parameter to NULL
        hr := commandList.Reset(commandAllocator[frameIndex], pipelineStateObject);
        if (hr <> S_OK) then
        begin
            Running := False;
        end;

        // here we start recording commands into the commandList (which all the commands will be stored in the commandAllocator)

        // transition the "frameIndex" render target from the present state to the render target state so the command list draws to it starting from here
        ltest := TD3DX12_RESOURCE_BARRIER.CreateTransition(renderTargets[frameIndex], D3D12_RESOURCE_STATE_PRESENT,
            D3D12_RESOURCE_STATE_RENDER_TARGET);
        commandList.ResourceBarrier(1, @lTest);
        outputdebugStringA('Ex');
        // here we again get the handle to our current render target view so we can set it as the render target in the output merger stage of the pipeline
        rtvDescriptorHeap.GetCPUDescriptorHandleForHeapStart(rtvHandle);
        rtvHandle := TD3D12_CPU_DESCRIPTOR_HANDLE.Create(rtvHandle, frameIndex, rtvDescriptorSize);
        outputdebugStringA('Ey');
        // get a handle to the depth/stencil buffer
        dsDescriptorHeap.GetCPUDescriptorHandleForHeapStart(dsvHandle);

        // set the render target for the output merger stage (the output of the pipeline)
        commandList.OMSetRenderTargets(1, @rtvHandle, False, @dsvHandle);

        // Clear the render target by using the ClearRenderTargetView command
        commandList.ClearRenderTargetView(rtvHandle, TSingleArray4(clearColor), 0, nil);

        // clear the depth/stencil buffer
        dsDescriptorHeap.GetCPUDescriptorHandleForHeapStart(rtvHandle);
        commandList.ClearDepthStencilView(rtvHandle, D3D12_CLEAR_FLAG_DEPTH, 1.0, 0, 0, nil);

        // set root signature
        commandList.SetGraphicsRootSignature(rootSignature); // set the root signature


        // draw triangle
        commandList.RSSetViewports(1, @viewport); // set the viewports
        commandList.RSSetScissorRects(1, @scissorRect); // set the scissor rects
        commandList.IASetPrimitiveTopology(D3D_PRIMITIVE_TOPOLOGY_TRIANGLELIST); // set the primitive topology
        commandList.IASetVertexBuffers(0, 1, @vertexBufferView); // set the vertex buffer (using the vertex buffer view)
        commandList.IASetIndexBuffer(indexBufferView);

        // first cube

        // set cube1's constant buffer
        commandList.SetGraphicsRootConstantBufferView(0, constantBufferUploadHeaps[frameIndex].GetGPUVirtualAddress());

        // draw first cube
        commandList.DrawIndexedInstanced(numCubeIndices, 1, 0, 0, 0);

        // second cube

        // set cube2's constant buffer. You can see we are adding the size of ConstantBufferPerObject to the constant buffer
        // resource heaps address. This is because cube1's constant buffer is stored at the beginning of the resource heap, while
        // cube2's constant buffer data is stored after (256 bits from the start of the heap).
        commandList.SetGraphicsRootConstantBufferView(0, constantBufferUploadHeaps[frameIndex].GetGPUVirtualAddress() +
            ConstantBufferPerObjectAlignedSize);

        // draw second cube
        commandList.DrawIndexedInstanced(numCubeIndices, 1, 0, 0, 0);


        // transition the "frameIndex" render target from the render target state to the present state. If the debug layer is enabled, you will receive a
        // warning if present is called on the render target when it's not in the present state
        ltest := TD3DX12_RESOURCE_BARRIER.CreateTransition(renderTargets[frameIndex], D3D12_RESOURCE_STATE_RENDER_TARGET,
            D3D12_RESOURCE_STATE_PRESENT);
        commandList.ResourceBarrier(1, @ltest);

        hr := commandList.Close();
        if (hr <> S_OK) then
        begin
            Running := False;
        end;
    end;



    procedure Render();
    var
        hr: HRESULT;
        ppCommandLists: array [0..0] of ID3D12CommandList;
    begin
        UpdatePipeline(); // update the pipeline by sending commands to the commandqueue

        // create an array of command lists (only one command list here)
        ppCommandLists[0] := commandList;

        // execute the array of command lists
        commandQueue.ExecuteCommandLists(Length(ppCommandLists), @ppCommandLists[0]);

        // this command goes in at the end of our command queue. we will know when our command queue
        // has finished because the fence value will be set to "fenceValue" from the GPU since the command
        // queue is being executed on the GPU
        hr := commandQueue.Signal(fence[frameIndex], fenceValue[frameIndex]);
        if (hr <> S_OK) then
        begin
            Running := False;
        end;

        // present the current backbuffer
        hr := swapChain.Present(0, 0);
        if (hr <> S_OK) then
        begin
            Running := False;
        end;
    end;



    procedure Cleanup();
    var
        i: integer;
        fs: longbool = False;
    begin
        // wait for the gpu to finish all frames
        for  i := 0 to frameBufferCount - 1 do
        begin
            frameIndex := i;
            WaitForPreviousFrame();
        end;

        // get swapchain out of full screen before exiting
        if (swapChain.GetFullscreenState(fs, nil)) = S_OK then
            swapChain.SetFullscreenState(False, nil);

        SAFE_RELEASE(device);
        SAFE_RELEASE(swapChain);
        SAFE_RELEASE(commandQueue);
        SAFE_RELEASE(rtvDescriptorHeap);
        SAFE_RELEASE(commandList);

        for i := 0 to frameBufferCount - 1 do
        begin
            SAFE_RELEASE(renderTargets[i]);
            SAFE_RELEASE(commandAllocator[i]);
            SAFE_RELEASE(fence[i]);
        end;

        SAFE_RELEASE(pipelineStateObject);
        SAFE_RELEASE(rootSignature);
        SAFE_RELEASE(vertexBuffer);
        SAFE_RELEASE(indexBuffer);

        SAFE_RELEASE(depthStencilBuffer);
        SAFE_RELEASE(dsDescriptorHeap);

        for i := 0 to frameBufferCount - 1 do
            SAFE_RELEASE(constantBufferUploadHeaps[i]);

    end;



    procedure mainloop();
    var
        msg: TMSG;
    begin
        ZeroMemory(@msg, sizeof(MSG));

        while (Running) do
        begin
            if (PeekMessage(msg, 0, 0, 0, PM_REMOVE)) then
            begin
                if (msg.message = WM_QUIT) then
                    break;

                TranslateMessage(msg);
                DispatchMessage(msg);
            end
            else
            begin
                // run game code
                Update(); // update the game logic
                Render(); // execute the command queue (rendering the scene is the result of the gpu executing the command lists)
            end;
        end;
    end;


begin
    // create the window
    if (not InitializeWindow(hInstance, SW_SHOW, Width, Height, FullScreen)) then
    begin
        MessageBox(0, 'Window Initialization - Failed',
            'Error', MB_OK);
        Exit;
    end;
    // initialize direct3d
    if (not InitD3D()) then
    begin
        MessageBox(0, 'Failed to initialize direct3d 12',
            'Error', MB_OK);
        Cleanup();
        Exit;
    end;


    // start the main loop
    mainloop();

    // we want to wait for the gpu to finish executing the command list before we start releasing everything
    WaitForPreviousFrame();

    // close the fence event
    CloseHandle(fenceEvent);

    // clean up everything
    Cleanup();

end.
