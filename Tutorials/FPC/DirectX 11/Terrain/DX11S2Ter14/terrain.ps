////////////////////////////////////////////////////////////////////////////////
// Filename: terrain.ps
////////////////////////////////////////////////////////////////////////////////


//////////////
// TEXTURES //
//////////////
Texture2D diffuseTexture1 : register(t0);
Texture2D normalTexture1 : register(t1);
Texture2D normalTexture2 : register(t2);
Texture2D normalTexture3 : register(t3);


//////////////
// SAMPLERS //
//////////////
SamplerState SampleType : register(s0);


//////////////////////
// CONSTANT BUFFERS //
//////////////////////
cbuffer LightBuffer
{
    float4 diffuseColor;
    float3 lightDirection;
    float padding;
};


//////////////
// TYPEDEFS //
//////////////
struct PixelInputType
{
    float4 position : SV_POSITION;
    float2 tex : TEXCOORD0;
	float3 normal : NORMAL;
	float3 tangent : TANGENT;
	float3 binormal : BINORMAL;
	float4 color : COLOR;
	float2 tex2 : TEXCOORD1;
	float4 depthPosition : TEXCOORD2;
};


////////////////////////////////////////////////////////////////////////////////
// Pixel Shader
////////////////////////////////////////////////////////////////////////////////
float4 TerrainPixelShader(PixelInputType input) : SV_TARGET
{
 	float slope;
	float3 lightDir;
    float4 textureColor1;
	float4 textureColor2;
	float4 bumpMap;
	float3 bumpNormal;
    float lightIntensity;
	float4 material1;
	float4 material2;
	float blendAmount;
	float4 color;
	float depthValue;


	// Calculate the slope of this point.
    slope = 1.0f - input.normal.y;

	// Get the depth value of the pixel by dividing the Z pixel depth by the homogeneous W coordinate.
    depthValue = input.depthPosition.z / input.depthPosition.w;

    // Invert the light direction for calculations.
    lightDir = -lightDirection;
	
	// Setup the first material.
    textureColor1 = diffuseTexture1.Sample(SampleType, input.tex);

	// Select the normal map for the first material based on the distance.
	if(depthValue > 0.998f)
	{
	    bumpMap = normalTexture3.Sample(SampleType, input.tex2);
	}
	else
	{
		bumpMap = normalTexture1.Sample(SampleType, input.tex);
	}

	bumpMap = (bumpMap * 2.0f) - 1.0f;
	bumpNormal = (bumpMap.x * input.tangent) + (bumpMap.y * input.binormal) + (bumpMap.z * input.normal);
	bumpNormal = normalize(bumpNormal);
	lightIntensity = saturate(dot(bumpNormal, lightDir));
    material1 = saturate(textureColor1 * lightIntensity);
	
	// Setup the second material.
	textureColor2 = float4(1.0f, 1.0f, 1.0f, 1.0f);  // Snow color.
	bumpMap = normalTexture2.Sample(SampleType, input.tex);
	bumpMap = (bumpMap * 2.0f) - 1.0f;
    bumpNormal = (bumpMap.x * input.tangent) + (bumpMap.y * input.binormal) + (bumpMap.z * input.normal);
    bumpNormal = normalize(bumpNormal);
    lightIntensity = saturate(dot(bumpNormal, lightDir));
    material2 = saturate(textureColor2 * lightIntensity);

	// Determine which material to use based on slope.
    if(slope < 0.2)
    {
        blendAmount = slope / 0.2f;
        color = lerp(material2, material1, blendAmount);
    }
	if(slope >= 0.2) 
    {
        color = material1;
    }

    return color;
}